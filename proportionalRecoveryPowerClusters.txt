model{
        for (nn in 1:NMeasurements){
            y[nn] ~ dnorm(ym[nn],yp)T(0,66) # Model fit to data
            ym[nn] <- max(min(ymm[nn],66),0)
            ymm[nn] <- alpha[id[nn]] + (r[g[id[nn]]]*(66-alpha[id[nn]])) * (1-exp(-t[nn]/tau[g[id[nn]]]))+ stEff[id[nn]]* (1-exp(-max(t[nn]-tStart[id[nn]],0)/tau[g[id[nn]]])) # Model estimate
        }
        for (ss in 1:NSubjects) {
            alpha[ss] <- alphaT[ss] * 66 # Patient specific intercept in range 0-66
            logit(alphaT[ss]) <- alphaL[ss] # Patient specific intercept in range 0-1
            alphaL[ss] ~ dnorm(alpham[g[ss]],alphap[g[ss]]) # Patient specific intercept in range -inf +inf
            g[ss] ~ dcat(gp[]) # Group assignment for a patient
            c[ss] <- clust[g[ss]]
            stEff[ss] <- trEff[c[ss]]*trGr[ss] + plEff
        }
        
        for (cc in 1:NClusters){
            trEff[cc] ~ dnorm(0,10E-2)T(-22,22)
            p[cc] <- (trEff[cc] > 0)
        }
        plEff ~ dnorm(0,10E-2)T(-22,22)
        gagr <- (sum(g == gorg))/NSubjects
        cagr <- (sum(c == corg))/NSubjects
        yp ~ dgamma(3.33,0.67)
        ys <- pow(yp,-0.5)
}